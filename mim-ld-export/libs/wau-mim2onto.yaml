title: MIM conversie
version: 0.1
# prefixes:
#   owl: http://www.w3.org/2002/07/owl#
#   skos: http://www.w3.org/2004/02/skos/core#
#   rdfs: http://www.w3.org/2000/01/rdf-schema#
#   sh: http://www.w3.org/ns/shacl#
#   dct: http://purl.org/dc/terms/
#   mim-item-ns: http://ontology.mim-namespace.org/def/modelname#
#   mim-model-ns: http://ontology.mim-namespace.org/
queries:
  - title: preset baseURI('s)
    query: >
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      INSERT {
        GRAPH <urn:uri> {
          ?im mim-ext:baseURI ?baseURI ;
              mim-ext:ontologyURI ?ontologyURI ;
              mim:modelElement ?elem .
        }
      }
      WHERE {
        # ALLE modelementen krijgen dezelfde URI
        # ontologie krijgt baseURI voor '#', als die er is.
        GRAPH <urn:input> {
          ?im a mim:Informatiemodel ;
            mim-ext:baseURI ?baseURI .
          ?package mim:bevatModelelement ?elem .

          BIND(SUBSTR(STR(?baseURI), 1, (STRLEN(?baseURI)-1)) as ?ontologyURI)
        }
      }
  - title: create technical names
    query: >
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      INSERT {
        GRAPH <urn:input> {
          ?item skos:notation ?codefixed
        }
      }
      WHERE {
        GRAPH <urn:input> {
          ?item mim:naam ?naam
          BIND (replace(?naam,"[^a-zA-Z0-9_-]","_") as ?codefixed)
        }
      }
  - title: convert mim:Package (and subclasses of mim:Package)
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      PREFIX dct: <http://purl.org/dc/terms/>
      INSERT {
        GRAPH <urn:output> {
          ?ontology a owl:Ontology.
          ?ontology rdfs:label ?naam.
          ?ontology skos:notation ?code.
          ?ontology rdfs:seeAlso ?package.
        }
        #
        #GRAPH <urn:uri> {
        #  ?package dct:identifier ?ontology .
        #}
        #
      }
      WHERE {
        # SELECT (iri(concat("http://ontology.mim-namespace.org/",?code)) as ?ontology) ?code ?naam ?package
        SELECT (IRI(CONCAT(?ontologyURI, "/", ?code))) ?code ?naam ?package
        WHERE {
          GRAPH <urn:input> {
            ?package a ?packagetype.
            ?package mim:naam ?naam.
            ?package skos:notation ?code.
            FILTER (?packagetype=mim:Informatiemodel || ?packagetype=mim:Domein)
          }
          GRAPH <urn:uri> {
            ?im mim-ext:ontologyURI ?ontologyURI .
          }
        }
      }
  - title: convert mim:Objecttype
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?subject a owl:Class, sh:NodeShape.
          ?subject sh:targetClass ?subject.
          ?subject rdfs:label ?naam.
          ?subject skos:notation ?code.
          ?subject rdfs:seeAlso ?objecttype.
        }
      }
      WHERE {
        SELECT ?subject ?code ?naam ?objecttype
        WHERE {
          GRAPH <urn:input> {
            ?objecttype a mim:Objecttype.
            ?objecttype mim:naam ?naam.
            ?objecttype skos:notation ?code.
            OPTIONAL { ?objecttype mim-ext:uri ?uri }
          }
          GRAPH <urn:uri> {
            ?im mim-ext:baseURI ?baseURI .
          }
          BIND(IRI(COALESCE(?uri, concat(?baseURI,?code))) as ?subject)
        }
      }
  - title: convert mim:Gegevensgroeptype
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?subject a owl:Class, sh:NodeShape.
          ?subject sh:targetClass ?subject.
          ?subject rdfs:label ?naam.
          ?subject skos:notation ?code.
          ?subject rdfs:seeAlso ?gegevensgroeptype.
        }
      }
      WHERE {
        SELECT ?subject ?code ?naam ?gegevensgroeptype
        WHERE {
          GRAPH <urn:input> {
            ?gegevensgroeptype a mim:Gegevensgroeptype.
            ?gegevensgroeptype mim:naam ?naam.
            ?gegevensgroeptype skos:notation ?code.
            OPTIONAL { ?gegevensgroeptype mim-ext:uri ?uri }
          }
          GRAPH <urn:uri> {
            ?im mim-ext:baseURI ?baseURI .
          }
          BIND(IRI(COALESCE(?uri, concat(?baseURI,?code))) as ?subject)
        }
      }
  - title: convert mim:Relatieklasse
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?subject a owl:Class, sh:NodeShape.
          ?subject sh:targetClass ?subject.
          ?subject rdfs:label ?naam.
          ?subject skos:notation ?code.
          ?subject rdfs:seeAlso ?relatieklasse.
        }
      }
      WHERE {
        SELECT ?subject ?code ?naam ?relatieklasse
        WHERE {
          GRAPH <urn:input> {
            ?relatieklasse a mim:Relatieklasse.
            ?relatieklasse mim:naam ?naam.
            ?relatieklasse skos:notation ?code.
            OPTIONAL { ?relatieklasse mim-ext:uri ?uri }
          }
          GRAPH <urn:uri> {
            ?im mim-ext:baseURI ?baseURI .
          }
          BIND(IRI(COALESCE(?uri, concat(?baseURI,?code))) as ?subject)
        }
      }
  - title: convert mim:Attribuutsoort / mim:dataElement
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?property a owl:DatatypeProperty.
          ?property rdfs:label ?naam.
          ?property rdfs:seeAlso ?attribuutsoort.
          ?ownershape sh:property ?propertyShape.
          ?propertyShape a sh:PropertyShape.
          ?propertyShape sh:name ?code.
          ?propertyShape sh:path ?property.
          ?propertyShape rdfs:seeAlso ?attribuutsoort
        }
      }
      WHERE {
        SELECT ?property ?ownershape ?propertyShape ?code ?naam ?attribuutsoort
        WHERE {
          GRAPH <urn:input> {
            ?owner a ?mimtype.
            ?owner ?mimrel ?attribuutsoort.
            ?owner skos:notation ?ownercode.
            ?attribuutsoort a ?mimattr.
            ?attribuutsoort mim:naam ?naam.
            ?attribuutsoort skos:notation ?code.

            OPTIONAL { ?owner mim-ext:uri ?ownerURI }
            OPTIONAL { ?attribuutsoort mim-ext:uri ?attribuutsoortURI }

            FILTER ((?mimattr = mim:Attribuutsoort && ?mimrel = mim:attribuut) || (?mimattr = mim:DataElement && ?mimrel = mim:dataElement))
            FILTER (?mimtype = mim:Objecttype || ?mimtype = mim:Gegevensgroeptype || ?mimtype = mim:Relatieklasse || ?mimtype = mim:GestructureerdDatatype)
          }
          GRAPH <urn:uri> {
            ?im mim-ext:baseURI ?baseURI .
          }           
          BIND(IRI(COALESCE(?attribuutsoortURI, concat(?baseURI,?code))) as ?property)
          BIND(IRI(COALESCE(?ownerURI, concat(?baseURI,?ownercode))) as ?ownershape)
          BIND(IRI(CONCAT(COALESCE(?ownerURI, concat(?baseURI,?ownercode)),".",?code)) as ?propertyShape)
          #Can result in minting new URI's in external domains. 
        }
      }
  - title: convert mim:Relatiesoort
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?property a owl:ObjectProperty.
          ?property rdfs:label ?naam.
          ?property rdfs:seeAlso ?relatiesoort.
          ?owner sh:property ?propertyShape.
          ?propertyShape a sh:PropertyShape.
          ?propertyShape sh:name ?code.
          ?propertyShape sh:path ?property.
          ?propertyShape sh:nodeKind sh:IRI.
          ?propertyShape sh:class ?class.
          ?propertyShape rdfs:seeAlso ?relatiesoort
        }
      }
      WHERE {
        SELECT  ?property ?propertyShape ?owner ?class ?code ?naam ?relatiesoort
        WHERE {
          GRAPH <urn:input> {
            ?relatiesoort a mim:Relatiesoort.
            ?relatiesoort mim:naam ?naam.
            ?relatiesoort mim:bron ?bron.
            ?relatiesoort mim:doel ?doel.
            ?relatiesoort skos:notation ?code.
            ?bron skos:notation ?ownercode.
            ?doel skos:notation ?classcode.

            OPTIONAL { ?relatiesoort mim-ext:uri ?relatiesoortURI }
            OPTIONAL { ?bron mim-ext:uri ?ownerURI }
            OPTIONAL { ?doel mim-ext:uri ?classURI }

            FILTER NOT EXISTS {
              ?relatiesoort mim:relatierol ?relatieroldoel.
              ?relatieroldoel a mim:RelatierolDoel.
              ?relatieroldoel mim:naam ?relatieroldoelnaam
            }
          }
          GRAPH <urn:uri> {
            ?im mim-ext:baseURI ?baseURI .
          }
          BIND(IRI(COALESCE(?attribuutsoortURI, concat(?baseURI,?code))) as ?property)
          BIND(IRI(COALESCE(?ownerURI, concat(?baseURI,?ownercode))) as ?owner)
          BIND(IRI(CONCAT(COALESCE(?ownerURI, concat(?baseURI,?ownercode)),".",?code)) as ?propertyShape)
          BIND(IRI(COALESCE(?classURI, concat(?baseURI,?classcode))) as ?class)
          #Can result in minting new URI's in external domains. 
        }
      }
  - title: convert mim:ExterneKoppeling
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?property a owl:ObjectProperty.
          ?property rdfs:label ?naam.
          ?property rdfs:seeAlso ?externekoppeling.
          ?owner sh:property ?propertyShape.
          ?propertyShape a sh:PropertyShape.
          ?propertyShape sh:name ?code.
          ?propertyShape sh:path ?property.
          ?propertyShape sh:nodeKind sh:IRI.
          ?propertyShape sh:class ?class.
          ?propertyShape rdfs:seeAlso ?externekoppeling
        }
      }
      WHERE {
        SELECT ?property ?propertyShape ?owner ?class ?code ?naam ?externekoppeling
        WHERE {
          GRAPH <urn:input> {
            ?externekoppeling a mim:ExterneKoppeling.
            ?externekoppeling mim:naam ?naam.
            ?externekoppeling mim:bron ?bron.
            ?externekoppeling mim:doel ?doel.
            ?externekoppeling skos:notation ?code.
            ?bron skos:notation ?ownercode.
            ?doel skos:notation ?classcode.
            
            OPTIONAL { ?externekoppeling mim-ext:uri ?externekoppelingURI }
            OPTIONAL { ?bron mim-ext:uri ?ownerURI }
            OPTIONAL { ?doel mim-ext:uri ?classURI }
          }
          GRAPH <urn:uri> {
            ?im mim-ext:baseURI ?baseURI .
          }
          BIND(IRI(COALESCE(?externekoppelingURI, concat(?baseURI,?code))) as ?property)
          BIND(IRI(COALESCE(?ownerURI, concat(?baseURI,?ownercode))) as ?owner)
          BIND(IRI(CONCAT(COALESCE(?ownerURI, concat(?baseURI,?ownercode)),".",?code)) as ?propertyShape)
          BIND(IRI(COALESCE(?classURI, concat(?baseURI,?classcode))) as ?class)
          #Can result in minting new URI's in external domains. 
        }
      }
  - title: convert mim:PrimitiefDatatype
    query: >
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?subject a rdf:Datatype.
          ?subject rdfs:label ?naam.
          ?subject rdfs:seeAlso ?type.
        }
      }
      WHERE {
        SELECT ?subject ?naam ?type
        WHERE {
          GRAPH <urn:input> {
            ?type a mim:PrimitiefDatatype.
            ?type mim:naam ?naam.
            ?type skos:notation ?code.
            OPTIONAL { ?type mim-ext:uri ?uri }
          }
          GRAPH <urn:uri> {
            ?im mim-ext:baseURI ?baseURI .
          }
          BIND(IRI(COALESCE(?uri, concat(?baseURI,?code))) as ?subject)
        }
      }
  - title: convert mim:GestructureerdDatatype
    query: >
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?subject a owl:Class, sh:NodeShape.
          ?subject sh:targetClass ?datatype.
          ?subject rdfs:label ?naam.
          ?subject skos:notation ?code.
          ?subject rdfs:seeAlso ?type.
        }
      }
      WHERE {
        SELECT ?subject ?naam ?type
        WHERE {
          GRAPH <urn:input> {
            ?type a mim:GestructureerdDatatype.
            ?type mim:naam ?naam.
            ?type skos:notation ?code.
            OPTIONAL { ?type mim-ext:uri ?uri }
          }
          GRAPH <urn:uri> {
            ?im mim-ext:baseURI ?baseURI .
          }
          BIND(IRI(COALESCE(?uri, concat(?baseURI,?code))) as ?subject)
        }
      }
  - title: convert mim:Enumeratie
    query: >
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?subject a owl:Class, sh:NodeShape.
          ?subject sh:targetClass ?enumeratie.
          ?subject rdfs:subClassOf skos:Concept.
          ?subject rdfs:label ?naam.
          ?subject skos:notation ?code.
          ?subject rdfs:seeAlso ?type.
        }
      }
      WHERE {
        SELECT ?subject ?naam ?type
        WHERE {
          GRAPH <urn:input> {
            ?type a mim:Enumeratie.
            ?type mim:naam ?naam.
            ?type skos:notation ?code.
            OPTIONAL { ?type mim-ext:uri ?uri }
          }
          GRAPH <urn:uri> {
            ?im mim-ext:baseURI ?baseURI .
          }
          BIND(IRI(COALESCE(?uri, concat(?baseURI,?code))) as ?subject)
        }
      }
  - title: convert mim:Enumeratiewaarde
    query: >
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?item a ?type.
          ?item rdfs:label ?naam.
          ?item rdfs:seeAlso ?waarde.
        }
      }
      WHERE {
        SELECT ?type ?item ?naam ?waarde
        WHERE {
          GRAPH <urn:input> {
            ?enumeratie mim:waarde ?waarde.
            ?enumeratie skos:notation ?enum.
            ?waarde a mim:Enumeratiewaarde.
            ?waarde mim:naam ?naam.
            ?waarde skos:notation ?code.

            OPTIONAL { ?enumeratie mim-ext:uri ?typeURI }
            OPTIONAL { ?waarde mim-ext:uri ?waardeURI }
          }
          GRAPH <urn:uri> {
            ?im mim-ext:baseURI ?baseURI .
          }
          BIND(IRI(COALESCE(?typeURI, concat(?baseURI,?enum))) as ?type)
          BIND(IRI(COALESCE(?waardeURI, concat(?baseURI,?code))) as ?item)
        }
      }
  - title: convert mim:RelatierolBron
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?property a owl:ObjectProperty.
          ?property rdfs:label ?naam.
          ?property rdfs:seeAlso ?relatierolbron.
          ?owner sh:property ?propertyShape.
          ?propertyShape a sh:PropertyShape.
          ?propertyShape sh:name ?code.
          ?propertyShape sh:path ?property.
          ?propertyShape sh:nodeKind sh:IRI.
          ?propertyShape sh:class ?class.
          ?propertyShape rdfs:seeAlso ?relatierolbron
        }
      }
      WHERE {
        SELECT  ?property ?propertyShape ?owner ?class ?code ?naam ?relatierolbron
        WHERE {
          GRAPH <urn:input> {
            ?relatierolbron a mim:RelatierolBron.
            ?relatierolbron mim:naam ?naam.
            ?relatierolbron skos:notation ?code.
            ?relatiesoort mim:relatierol ?relatierolbron.
            ?relatiesoort mim:bron ?bron.
            ?relatiesoort mim:doel ?doel.
            ?doel skos:notation ?ownercode.
            ?bron skos:notation ?classcode.
            
            OPTIONAL { ?relatierolbron mim-ext:uri ?relatierolbronURI }
            OPTIONAL { ?doel mim-ext:uri ?ownerURI }
            OPTIONAL { ?bron mim-ext:uri ?classURI }
          }
          GRAPH <urn:uri> {
            ?im mim-ext:baseURI ?baseURI .
          }
          BIND(IRI(COALESCE(?relatierolbronURI, concat(?baseURI,?code))) as ?property)
          BIND(IRI(COALESCE(?ownerURI, concat(?baseURI,?ownercode))) as ?owner)
          BIND(IRI(CONCAT(COALESCE(?ownerURI, concat(?baseURI,?ownercode)),".",?code)) as ?propertyShape)
          BIND(IRI(COALESCE(?classURI, concat(?baseURI,?classcode))) as ?class)
          #Can result in minting new URI's in external domains. 
        }
      }
  - title: convert mim:RelatierolDoel
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?property a owl:ObjectProperty.
          ?property rdfs:label ?naam.
          ?property rdfs:seeAlso ?relatieroldoel.
          ?owner sh:property ?propertyShape.
          ?propertyShape a sh:PropertyShape.
          ?propertyShape sh:name ?code.
          ?propertyShape sh:path ?property.
          ?propertyShape sh:nodeKind sh:IRI.
          ?propertyShape sh:class ?class.
          ?propertyShape rdfs:seeAlso ?relatieroldoel.
          ?propertyShape mim:equivalent ?relatiesoort.
        }
      }
      WHERE {
        SELECT ?property ?propertyShape ?owner ?class ?code ?naam ?relatieroldoel ?relatiesoort
        WHERE {
          GRAPH <urn:input> {
            ?relatieroldoel a mim:RelatierolDoel.
            ?relatieroldoel mim:naam ?naam.
            ?relatieroldoel skos:notation ?code.
            ?relatiesoort mim:relatierol ?relatieroldoel.
            ?relatiesoort mim:bron ?bron.
            ?relatiesoort mim:doel ?doel.
            ?doel skos:notation ?classcode.
            ?bron skos:notation ?ownercode.
            
            OPTIONAL { ?relatieroldoel mim-ext:uri ?relatieroldoelURI }
            OPTIONAL { ?bron mim-ext:uri ?ownerURI }
            OPTIONAL { ?doel mim-ext:uri ?classURI }
          }
          GRAPH <urn:uri> {
            ?im mim-ext:baseURI ?baseURI .
          }
          BIND(IRI(COALESCE(?relatierolbronURI, concat(?baseURI,?code))) as ?property)
          BIND(IRI(COALESCE(?ownerURI, concat(?baseURI,?ownercode))) as ?owner)
          BIND(IRI(CONCAT(COALESCE(?ownerURI, concat(?baseURI,?ownercode)),".",?code)) as ?propertyShape)
          BIND(IRI(COALESCE(?classURI, concat(?baseURI,?classcode))) as ?class)
          #Can result in minting new URI's in external domains. 
        }
      }
  - title: convert mim:RelatierolDoel of mim:Relatieklasse
    query: >
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?statement rdfs:subClassOf rdf:Statement.
          ?statement sh:property ?subjectps.
          ?statement sh:property ?predicateps.
          ?statement sh:property ?objectps.
          ?predicateps a sh:PropertyShape.
          ?predicateps sh:path rdf:predicate.
          ?predicateps sh:hasValue ?predicate.
          ?predicateps sh:minCount 1 .
          ?predicateps sh:maxCount 1 .
          ?subjectps a sh:PropertyShape.
          ?subjectps sh:path rdf:subject.
          ?subjectps sh:nodeKind sh:IRI.
          ?subjectps sh:class ?subject.
          ?objectps a sh:PropertyShape.
          ?objectps sh:path rdf:object.
          ?objectps sh:nodeKind sh:IRI.
          ?objectps sh:class ?object.
        }
      }
      WHERE {
        SELECT  ?statement ?object ?subject ?predicate ?objectps ?subjectps ?predicateps ?code ?naam ?relatieroldoel
        WHERE {
          GRAPH <urn:input> {
            ?relatieroldoel a mim:RelatierolDoel.
            ?relatieroldoel skos:notation ?predicatecode.
            ?relatieklasse a mim:Relatieklasse.
            ?relatieklasse mim:relatierol ?relatieroldoel.
            ?relatieklasse mim:bron ?bron.
            ?relatieklasse mim:doel ?doel.
            ?relatieklasse skos:notation ?code.
            ?doel skos:notation ?objectcode.
            ?bron skos:notation ?subjectcode.
            
            OPTIONAL { ?relatieklasse mim-ext:uri ?statementURI }
            OPTIONAL { ?bron mim-ext:uri ?subjectURI }
            OPTIONAL { ?relatieroldoel mim-ext:uri ?predicateURI }
            OPTIONAL { ?doel mim-ext:uri ?objectURI }
          }
          GRAPH <urn:uri> {
            ?im mim-ext:baseURI ?baseURI .
          }
          BIND(IRI(COALESCE(?statementURI, concat(?baseURI,?code))) as ?statement)
          BIND(IRI(COALESCE(?objectURI, concat(?baseURI,?objectcode))) as ?object)
          BIND(IRI(COALESCE(?subjectURI, concat(?baseURI,?subjectcode))) as ?subject)
          BIND(IRI(COALESCE(?predicateURI, concat(?baseURI,?predicatecode))) as ?predicate)
          BIND(IRI(CONCAT(COALESCE(?statementURI, concat(?baseURI,?code)), ".object")) as ?objectps)
          BIND(IRI(CONCAT(COALESCE(?statementURI, concat(?baseURI,?code)), ".subject")) as ?subjectps)
          BIND(IRI(CONCAT(COALESCE(?statementURI, concat(?baseURI,?code)), ".predicate")) as ?predicateps)
          #Can result in minting new URI's in external domains. 
        }
      }
  - title: convert mim:Gegevensgroep
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?property a owl:ObjectProperty.
          ?property rdfs:label ?naam.
          ?property rdfs:seeAlso ?attribuutsoort.
          ?ownershape sh:property ?propertyShape.
          ?propertyShape a sh:PropertyShape.
          ?propertyShape sh:name ?code.
          ?propertyShape sh:path ?property.
          ?propertyShape sh:nodeKind sh:BlankNode.
          ?propertyShape sh:class ?class.
          ?propertyShape rdfs:seeAlso ?gegevensgroep
        }
      }
      WHERE {
        SELECT   ?property ?propertyShape ?ownershape ?class ?code ?naam ?gegevensgroep
        WHERE {
          GRAPH <urn:input> {
            ?owner a mim:Objecttype.
            ?owner mim:gegevensgroep ?gegevensgroep.
            ?owner skos:notation ?ownercode.
            ?gegevensgroep a mim:Gegevensgroep.
            ?gegevensgroep mim:naam ?naam.
            ?gegevensgroep mim:gegevensgroeptype ?type.
            ?gegevensgroep skos:notation ?code.
            ?type skos:notation ?typecode.
            
            OPTIONAL { ?owner mim-ext:uri ?ownerURI }
            OPTIONAL { ?gegevensgroep mim-ext:uri ?gegevensgroepURI }
            OPTIONAL { ?type mim-ext:uri ?typeURI }
          }
          GRAPH <urn:uri> {
            ?im mim-ext:baseURI ?baseURI .
          }
          BIND(IRI(COALESCE(?gegevensgroepURI, concat(?baseURI,?code))) as ?property)
          BIND(IRI(CONCAT(COALESCE(?ownerURI, concat(?baseURI,?ownercode)), ?code)) as ?propertyShape)
          BIND(IRI(COALESCE(?ownerURI, concat(?baseURI,?ownercode))) as ?ownershape)
          BIND(IRI(COALESCE(?typeURI, concat(?baseURI,?typecode))) as ?class)
          #Can result in minting new URI's in external domains. 
        }
      }
  - title: convert mim:Referentielijst
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?subject a owl:Class, sh:NodeShape.
          ?subject sh:targetClass ?subject.
          ?subject rdfs:label ?naam.
          ?subject skos:notation ?code.
          ?subject rdfs:seeAlso ?referentielijst.
        }
      }
      WHERE {
        SELECT  ?subject ?code ?naam ?referentielijst
        WHERE {
          GRAPH <urn:input> {
            ?referentielijst a mim:Referentielijst.
            ?referentielijst mim:naam ?naam.
            ?referentielijst skos:notation ?code.
            
            OPTIONAL { ?referentielijst mim-ext:uri ?uri }
          }
          GRAPH <urn:uri> {
            ?im mim-ext:baseURI ?baseURI .
          }
          BIND(IRI(COALESCE(?uri, concat(?baseURI,?code))) as ?subject)
        }
      }
  - title: convert mim:Codelijst
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?subject a owl:Class, sh:NodeShape.
          ?subject sh:targetClass ?subject.
          ?subject rdfs:label ?naam.
          ?subject skos:notation ?code.
          ?subject rdfs:seeAlso ?codelijst.
        }
      }
      WHERE {
        SELECT ?subject ?code ?naam ?codelijst
        WHERE {
          GRAPH <urn:input> {
            ?codelijst a mim:Codelijst.
            ?codelijst mim:naam ?naam.
            ?codelijst skos:notation ?code.
            
            OPTIONAL { ?codelijst mim-ext:uri ?uri }
          }
          GRAPH <urn:uri> {
            ?im mim-ext:baseURI ?baseURI .
          }
          BIND(IRI(COALESCE(?uri, concat(?baseURI,?code))) as ?subject)
        }
      }
  - title: convert mim:Generalisatie
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      INSERT {
        GRAPH <urn:output> {
          ?subclass rdfs:subClassOf ?superclass
        }
      }
      WHERE {
        GRAPH <urn:input> {
          ?generalisatie a mim:Generalisatie.
          ?generalisatie mim:subtype ?subtype.
          ?generalisatie mim:supertype ?supertype.
        }
        GRAPH <urn:output> {
          ?subclass rdfs:seeAlso ?subtype.
          ?superclass rdfs:seeAlso ?supertype.
        }
      }
  - title: convert mim:begrip
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX dct: <http://purl.org/dc/terms/>
      INSERT {
        GRAPH <urn:output> {
          ?element dct:subject ?begrip
        }
      }
      WHERE {
        GRAPH <urn:input> {
          ?item mim:begrip ?begrip
        }
        GRAPH <urn:output> {
          ?element rdfs:seeAlso ?item
        }
      }
  - title: convert mim:definitie
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      INSERT {
        GRAPH <urn:output> {
          ?element rdfs:comment ?definitie
        }
      }
      WHERE {
        GRAPH <urn:input> {
          ?item mim:definitie ?definitie
        }
        GRAPH <urn:output> {
          ?element rdfs:seeAlso ?item
        }
      }
  - title: convert mim:formeelPatroon
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      INSERT {
        GRAPH <urn:output> {
          ?element sh:pattern ?patroon
        }
      }
      WHERE {
        GRAPH <urn:input> {
          ?item mim:formeelPatroon ?patroon
        }
        GRAPH <urn:output> {
          ?element rdfs:seeAlso ?item
        }
      }
  - title: convert mim:type (mim:PrimitiefDatatype)
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      INSERT {
        GRAPH <urn:output> {
          ?pshape sh:datatype ?datatype.
          ?pshape sh:nodeKind sh:Literal.
        }
      }
      WHERE {
        {
          GRAPH <urn:input> {
            ?item mim:type ?type.
            ?type a mim:PrimitiefDatatype.
            FILTER NOT EXISTS {?type owl:sameAs ?datatype}
          }
          GRAPH <urn:output> {
            ?pshape a sh:PropertyShape.
            ?pshape rdfs:seeAlso ?item.
            ?datatype rdfs:seeAlso ?type.
          }
        }
        UNION
        {
          GRAPH <urn:input> {
            ?item mim:type ?type.
            ?type a mim:PrimitiefDatatype.
            ?type owl:sameAs ?datatype
          }
          GRAPH <urn:output> {
            ?pshape a sh:PropertyShape.
            ?pshape rdfs:seeAlso ?item.
          }
        }
      }
  - title: convert mim:type (mim:GestructureerdDatatype)
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?pshape sh:class ?datatype.
          ?pshape sh:nodeKind sh:BlankNode.
        }
      }
      WHERE {
        GRAPH <urn:input> {
          ?item mim:type ?type.
          ?type a mim:GestructureerdDatatype.
        }
        GRAPH <urn:output> {
          ?pshape a sh:PropertyShape.
          ?pshape rdfs:seeAlso ?item.
          ?datatype rdfs:seeAlso ?type.
        }
      }
  - title: convert mim:type (mim:Enumeratie)
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?pshape sh:class ?enumeratie.
        }
      }
      WHERE {
        GRAPH <urn:input> {
          ?item mim:type ?type.
          ?type a mim:Enumeratie.
        }
        GRAPH <urn:output> {
          ?pshape a sh:PropertyShape.
          ?pshape rdfs:seeAlso ?item.
          ?enumeratie rdfs:seeAlso ?type.
        }
      }
  - title: convert mim:type (mim:Referentielijst)
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?pshape sh:class ?refclass
        }
      }
      WHERE {
        GRAPH <urn:input> {
          ?item mim:type ?type.
          ?type a mim:Referentielijst.
        }
        GRAPH <urn:output> {
          ?pshape a sh:PropertyShape.
          ?pshape rdfs:seeAlso ?item.
          ?refclass rdfs:seeAlso ?type.
        }
      }
  - title: convert mim:type (mim:Codelijst)
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?pshape sh:class ?refclass
        }
      }
      WHERE {
        GRAPH <urn:input> {
          ?item mim:type ?type.
          ?type a mim:Codelijst.
        }
        GRAPH <urn:output> {
          ?pshape a sh:PropertyShape.
          ?pshape rdfs:seeAlso ?item.
          ?refclass rdfs:seeAlso ?type.
        }
      }
  - title: change owl:DatatypeProperty to owl:ObjectProperty for non-primitive datatypes
    query: >
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      DELETE {
        GRAPH <urn:output> {
          ?property a owl:DatatypeProperty
        }
      }
      INSERT {
        GRAPH <urn:output> {
          ?property a owl:ObjectProperty
        }
      }
      WHERE {
        GRAPH <urn:output> {
          ?pshape a sh:PropertyShape.
          ?pshape sh:class ?class.
          ?pshape sh:path ?property.
        }
      }
  - title: convert mim:lengte (minimum)
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
      INSERT {
        GRAPH <urn:output> {
          ?pshape sh:minLength ?minlength.
        }
      }
      WHERE {
        {
          SELECT ?pshape (strdt(strbefore(concat(?lengte,".."),".."), xsd:integer) as ?minlength)
          WHERE {
            GRAPH <urn:input> {
              ?element mim:lengte ?lengte
            }
            GRAPH <urn:output> {
              ?pshape a sh:PropertyShape.
              ?pshape rdfs:seeAlso ?element
            }
          }
        }
        FILTER (?minlength>0)
      }
  - title: convert mim:lengte (maximum)
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
      INSERT {
        GRAPH <urn:output> {
          ?pshape sh:maxLength ?maxlength .
        }
      }
      WHERE {
        {
          SELECT ?pshape ?maxlength
          WHERE {
            GRAPH <urn:input> {
              ?element mim:lengte ?lengte
            }
            GRAPH <urn:output> {
              ?pshape a sh:PropertyShape.
              ?pshape rdfs:seeAlso ?element
            }
            BIND(
            IF(
              CONTAINS(?lengte, ".."),
              strdt(strafter(?lengte,".."), xsd:integer), 
              strdt(?lengte, xsd:integer)
            ) as ?maxLength )
          }
        }
        FILTER (?maxlength>0)
      }
  - title: convert mim:kardinaliteit (minimum)
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
      INSERT {
        GRAPH <urn:output> {
          ?pshape sh:minCount ?mincount.
        }
      }
      WHERE {
        {
          SELECT ?pshape (strdt(strbefore(concat(?kardinaliteit,".."),".."), xsd:integer) as ?mincount)
          WHERE {
            GRAPH <urn:input> {
              ?element mim:kardinaliteit ?kardinaliteit
            }
            GRAPH <urn:output> {
              ?pshape a sh:PropertyShape.
              ?pshape rdfs:seeAlso ?element
            }
          }
        }
        FILTER (?mincount>0)
      }
  - title: convert mim:kardinaliteit (maximum)
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
      INSERT {
        GRAPH <urn:output> {
      		?pshape sh:maxCount ?maxcount.
        }
      }
      WHERE {
        {
          SELECT ?pshape (strdt(replace(strafter(if(regex(?kardinaliteit,"\\.\\."),?kardinaliteit,concat(?kardinaliteit,"..",?kardinaliteit)),".."),"\\*","-1"), xsd:integer) as ?maxcount)
          WHERE {
            GRAPH <urn:input> {
              ?element mim:kardinaliteit ?kardinaliteit
            }
            GRAPH <urn:output> {
              {
                ?pshape a sh:PropertyShape.
                ?pshape rdfs:seeAlso ?element
              }
              UNION
              {
                ?pshape a sh:PropertyShape.
                ?pshape rdfs:seeAlso ?relatiesoort.
                ?relatiesoort mim:relatierol ?element
              }
            }
          }
        }
        FILTER (?maxcount>0)
      }
  - title: convert mim:bevatModelelement for mim:Packages
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?ontology owl:imports ?import
        }
      }
      WHERE {
        GRAPH <urn:input> {
          ?parent mim:bevatModelelement ?item.
        }
        GRAPH <urn:output> {
          ?ontology a owl:Ontology.
          ?import a owl:Ontology.
          ?ontology rdfs:seeAlso ?parent.
          ?import rdfs:seeAlso ?item.
        }
      }
  - title: convert mim:bevatModelelement for other modelelements
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX owl: <http://www.w3.org/2002/07/owl#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      INSERT {
        GRAPH <urn:output> {
          ?element rdfs:isDefinedBy ?ontology
        }
      }
      WHERE {
        GRAPH <urn:input> {
          ?parent mim:bevatModelelement ?item.
        }
        GRAPH <urn:output> {
          ?ontology a owl:Ontology.
          ?ontology rdfs:seeAlso ?parent.
          ?element rdfs:seeAlso ?item.
          ?element a ?elementtype
          FILTER (?elementtype=owl:Class || ?elementtype=owl:DatatypeProperty || ?elementtype=owl:ObjectProperty)
        }
      }
  - title: remove rdfs:seeAlso / Add mim:equivalent
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      DELETE {
        GRAPH <urn:output> {
          ?s rdfs:seeAlso ?o
        }
      }
      INSERT {
        GRAPH <urn:output> {
          ?s mim:equivalent ?o
        }
      }
      WHERE {
        GRAPH <urn:output> {
          ?s rdfs:seeAlso ?o
        }
      }
  - title: replace mim datatypes with xsd
    query: >
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX mim: <http://bp4mc2.org/def/mim#>
      PREFIX mim-ext: <https://wau.ldproxy.net/def/mim-ext#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
      DELETE {
        GRAPH <urn:output> {
          ?pshape sh:datatype ?mimType .
          ?mimType ?p ?o
        }
      }
      INSERT {
        GRAPH <urn:output> {
          ?pshape sh:datatype ?xsdType .
        }
      }
      WHERE {
        VALUES (?name ?xsdType) {
          ("CharacterString" xsd:string )
          ("Integer" xsd:integer )
          ("Real" xsd:float )
          ("Decimal" xsd:decimal )
          ("Boolean" xsd:boolean )
          ("Date" xsd:date )
          ("DateTime" xsd:dateTime )
          ("Year" xsd:gYear )
          ("Day" xsd:gDay )
          ("Month" xsd:gMonth )
          ("URI" xsd:anyURI )
        }
        GRAPH <urn:output> {
          ?pshape sh:datatype ?mimType .
          ?mimType ?p ?o . 
          ?mimType rdfs:label ?name .
        }
      }